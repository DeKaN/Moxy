apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28

        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    testImplementation project(':moxy-compiler')
    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.truth
    testImplementation deps.robolectric
    testImplementation deps.androidJunit
    testImplementation deps.compiletesting
    testImplementation deps.javapoet
}

project.archivesBaseName = 'moxy'
group = rootProject.ext.groupId // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId rootProject.ext.groupId
            artifactId 'moxy'
            version rootProject.ext.targetVersion
            name 'Moxy'
            modelVersion = '4.0.0'
            description = 'Moxy library for Android'
            url = 'https://github.com/moxy-community/'
            inceptionYear '2019'
            licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Moxy Community'
                    email 'moxy-community@yandex.ru'
                    organization = 'Moxy Community'
                    organizationUrl 'https://github.com/moxy-community/'
                }
            }
            scm {
                connection 'scm:git@github.com:moxy-community/Moxy.git'
                developerConnection 'scm:git@github.com:moxy-community/Moxy.git'
                url 'https://github.com/moxy-community/Moxy.git'
            }
        }
    }
}


version = getArtifactVersion('moxy') // Library version

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    // User and ApiKey stored in local.properties
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")

    configurations = ['archives']
    // Package info for Bintray
    pkg {
        repo = 'maven'
        name = 'moxy'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/moxy-community/Moxy'
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("gpgPassphrase")
            }
            mavenCentralSync {
                sync = true
                //[Default: true] Determines whether to sync the version to Maven Central.
                user = properties.getProperty("mavenUser") //OSS user token: mandatory
                password = properties.getProperty("mavenToken") //OSS user password: mandatory
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

// Dependency to call only bintrayUpload task
bintrayUpload.dependsOn install

// Tasks for sources and javadocs jars
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}