import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
    id 'dagger.hilt.android.plugin'
}

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "moxy.sample"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        disableEmptyStrategyCheck: 'false',
                        enableEmptyStrategyHelper: 'true',
                        defaultMoxyStrategy      : 'moxy.viewstate.strategy.AddToEndSingleStrategy'
                ]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias 'Moxy'
            keyPassword 'MoxyRelease'
            storePassword 'MoxyRelease'
            storeFile file('DemoReleaseKeystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    buildFeatures {
        viewBinding true
    }

    // we've placed source code in "kotlin folder" but you don't have to do that.
    sourceSets {
        main {
            java.srcDirs += "src/main/kotlin"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'

    // AndroidX KTX
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-rc1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Material Design
    implementation 'com.google.android.material:material:1.2.0'

    // HTTP client
    implementation 'io.ktor:ktor-client-android:1.3.2'
    implementation 'io.ktor:ktor-client-json-jvm:1.3.2'
    implementation 'io.ktor:ktor-client-serialization-jvm:1.3.2'

    // Image loader
    implementation("io.coil-kt:coil:0.11.0")

    implementation 'com.google.dagger:hilt-android:2.28.3-alpha'
    kapt 'com.google.dagger:hilt-android-compiler:2.28.3-alpha'

    // java.time and other stuff without third-party libraries
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    // Moxy
    // uncomment for test library from local sources
    //implementation project(':moxy-androidx')
    //implementation project(':moxy-ktx')
    //kapt project(':moxy-compiler')

    // uncomment for test library from maven
    def moxyVersion = targetVersion
    implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    implementation "com.github.moxy-community:moxy-ktx:$moxyVersion"
    kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"
}
